| - routines.mac - 
| Generic routines that may be used a lot. 
| ---------------------------- 
| Original Version by: Plazmic 
| Improved by: L124RD 
| Fixed by: DKAA


Sub BuyItems 
/declare l1 int local
/declare l2 int local

   |/echo buying ${Param0} items
   /for l1 1 to ${Param0} 
      |/click left merchant buy 
      /varset l2 ${Me.Cash}
      /shift /notify MerchantWnd MW_Buy_Button leftmouseup
      :BuyItems_Wait
         /delay 3
      /if (${l2}==${Me.Cash}) /goto :BuyItems_Wait
      /echo bought one...
   /next l1 
/return 


| ---------------------------- 


Sub SellItem 
/declare tmpslot int local
   :SellItem 
      /varset tmpslot ${FindItem[${Param0}].InvSlot}
      /if (!${tmpslot}) /goto :DoneSellItem
      /itemnotify ${tmpslot} leftmouseup
      /shift /notify MerchantWnd MW_Sell_Button leftmouseup
      | two second limit on the server
      /delay 2s
      /delay ${Math.Rand[3]}
   /goto :SellItem 
   :DoneSellItem 
/return 


| ---------------------------- 


Sub BackPedal 
   /if (!${Defined[BackPedal_var]}) {
   /declare BackPedal_var int global
   }
   /if (${Param0}==${BackPedal_var}) /return 
   /varset BackPedal_var ${Param0} 
   /if (${Param0}==1) /keypress down hold 
   /if (${Param0}==0 )/keypress down 
/return 


| ---------------------------- 


Sub AutoRun 
   /if (!${Defined[AutoRun_var]}) {
   /declare AutoRun_var int global
   }
   /if (${Param0}==${AutoRun_var}) /return 
   /varset AutoRun_var ${Param0} 
   /if (${Param0}==1) /keypress up hold 
   /if (${Param0}==0) /keypress up 
/return 


| ---------------------------- 


Sub GotoLoc 
/declare v91 int local
   /varcalc v91 ${Math.Rand[4]+1}
   :GotoLocLoop 
      /face loc ${Param0} 
      /if (${Defined[Param1]}) {
        /if (${Param1.Equal[u]}) /keypress u
      }
      /call AutoRun 1 
   /if (${Math.Distance[${Param0}]>${v91}) /goto :GotoLocLoop 
   /call AutoRun 0 
/return 


| ---------------------------- 


Sub GotoTarg 
/declare v91 int local
   /varcalc v91 ${Math.Rand[4]}+1
   :GotoTargLoop 
      /face nolook
      /if (${Defined[Param1]}) {
        /if (${Param1.Equal[u]}) /keypress u
      }
      /call AutoRun 1 
   /if (${Target.Distance}>${v91}) /goto :GotoTargLoop 
   /call AutoRun 0 
/return 

| ---------------------------- 

Sub FindCombiner
/declare v99 int local

   /for v99 1 to 8
      /if (${Me.Inventory[pack${v99}].Type.Equal[${Param0}]}) /return ${v99}
   /next v99

/return 99

| End of routines.mac
